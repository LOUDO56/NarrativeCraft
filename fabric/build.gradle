plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

repositories {
    mavenLocal()
    maven {
        url 'https://maven.kosmx.dev/'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesAPI', version: '2.6.1') {
        exclude group: 'dev.kosmx.player-anim', module: 'player-animation-lib'
        exclude group: 'dev.kosmx.player-anim', module: 'anim-core'
        exclude group: 'net.raphimc', module: 'NoteBlockLib'
    }
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesServer', version: '2.6.1') {
        exclude group: 'dev.kosmx.player-anim', module: 'player-animation-lib'
        exclude group: 'dev.kosmx.player-anim', module: 'anim-core'
        exclude group: 'net.raphimc', module: 'NoteBlockLib'
    }
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesMc', version: '2.6.1') {
        exclude group: 'dev.kosmx.player-anim', module: 'player-animation-lib'
        exclude group: 'dev.kosmx.player-anim', module: 'anim-core'
        exclude group: 'net.raphimc', module: 'NoteBlockLib'
    }
    compileOnly group: 'dev.kosmx.player-anim', name: 'player-animation-lib', version: '2.0.2+1.21.5'
    compileOnly group: 'dev.kosmx.player-anim', name: 'anim-core', version: '2.0.2+1.21.5'
    include(implementation "com.bladecoder.ink:blade-ink:1.2.0+nc")
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}