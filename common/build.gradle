plugins {
    id 'multiloader-common'
    id 'net.neoforged.moddev'
}

repositories {
    mavenLocal()
    maven {
        url 'https://maven.kosmx.dev/'
    }
}

neoForge {
    neoFormVersion = neo_form_version
    // Automatically enable AccessTransformers if the file exists
    def at = file('src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) {
        accessTransformers.from(at.absolutePath)
    }
    parchment {
        minecraftVersion = parchment_minecraft
        mappingsVersion = parchment_version
    }
}

dependencies {
    compileOnly group: 'org.spongepowered', name: 'mixin', version: '0.8.5'
    // fabric and neoforge both bundle mixinextras, so it is safe to use it in common
    compileOnly group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    annotationProcessor group: 'io.github.llamalad7', name: 'mixinextras-common', version: '0.3.5'
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesAPI', version: '2.6.1') {
        exclude group: 'dev.kosmx.player-anim', module: 'player-animation-lib'
        exclude group: 'dev.kosmx.player-anim', module: 'anim-core'
        exclude group: 'net.raphimc', module: 'NoteBlockLib'
    }
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesServer', version: '2.6.1') {
        exclude group: 'dev.kosmx.player-anim', module: 'player-animation-lib'
        exclude group: 'dev.kosmx.player-anim', module: 'anim-core'
        exclude group: 'net.raphimc', module: 'NoteBlockLib'
    }
    compileOnly (group: 'io.github.kosmx.emotes', name: 'emotesMc', version: '2.6.1') {
        exclude group: 'dev.kosmx.player-anim', module: 'player-animation-lib'
        exclude group: 'dev.kosmx.player-anim', module: 'anim-core'
        exclude group: 'net.raphimc', module: 'NoteBlockLib'
    }
    compileOnly group: 'dev.kosmx.player-anim', name: 'player-animation-lib', version: '2.0.2+1.21.5'
    compileOnly group: 'dev.kosmx.player-anim', name: 'anim-core', version: '2.0.2+1.21.5'
    implementation "com.bladecoder.ink:blade-ink:1.2.0+nc"
}

configurations {
    commonJava {
        canBeResolved = false
        canBeConsumed = true
    }
    commonResources {
        canBeResolved = false
        canBeConsumed = true
    }
}

artifacts {
    commonJava sourceSets.main.java.sourceDirectories.singleFile
    commonResources sourceSets.main.resources.sourceDirectories.singleFile
}

